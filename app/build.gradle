apply plugin: 'com.android.application'

def getVersionCode() {
    def versionFile = file('version.properties')
    if (versionFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionFile))
        def versionCode = versionProps['VERSION_CODE'].toInteger()
        def runTasks = gradle.startParameter.taskNames
        if (':app:assembleRelease' in runTasks ) {
            versionProps['VERSION_CODE'] = (++versionCode).toString()
            versionProps.store(versionFile.newWriter(), null)
        }
        return versionCode
    } else {
        throw new GradleException("Could not find version.properties!")
    }
}

def getVersionName() {
    def date = new Date()
    def versionName = date.format('yyyyMMdd_HHmmss')
    return versionName
}

def currentVersionCode = getVersionCode()
def currentVersionName = getVersionName() + "(" + currentVersionCode + ")"

android {
    signingConfigs {
        debug {
            storeFile file('D:\\ADEV\\GitWS\\g4(z8).jks')
            storePassword 'zzzzzzzz'
            keyAlias 'key0'
            keyPassword 'zzzzzzzz'
        }
        release {
            storeFile file('D:\\ADEV\\GitWS\\g4(z8).jks')
            storePassword 'zzzzzzzz'
            keyAlias 'key0'
            keyPassword 'zzzzzzzz'
        }
    }
    compileSdkVersion 31
    defaultConfig {
        applicationId "com.g4ap.slibfora"
        minSdkVersion 31
        targetSdkVersion 31
        versionCode currentVersionCode
        versionName currentVersionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '33.0.0 rc3'

    android.applicationVariants.all { variant ->
        if ("release" == variant.buildType.name) {
            variant.outputs.all  {
                outputFileName = "SLibForA_${defaultConfig.versionName}_${buildType.name}.apk"
            }
        }
    }

}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
}
